apply plugin: 'com.android.application'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'com.Arsylk.MammonsMite'
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 4
        versionName '1.3'
    }
    signingConfigs {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(rootProject.file('keystore.properties')))
        release {
            keyAlias keystoreProperties.getProperty('keyAlias')
            keyPassword keystoreProperties.getProperty('keyPassword')
            storeFile file(keystoreProperties.getProperty('storeFile'))
            storePassword keystoreProperties.getProperty('storePassword')
        }
    }
    applicationVariants.all { variant ->
        if (variant.name == 'github' || variant.name == 'googleplay') {
            variant.outputs.all { output ->
                def dateFormat = new Date().format('dd.MM.yyyy')
                output.outputFileName = "app-${variant.name}-v${variant.versionName}_${dateFormat}.apk"
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.release
            resValue('string', 'isSpecial', 'true')
        }
        release {
            signingConfig signingConfigs.release
            resValue('string', 'isSpecial', 'true')
        }
        googleplay {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            resValue('string', 'isSpecial', 'false')
        }
        github {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            resValue('string', 'isSpecial', 'true')
        }
    }
    productFlavors {
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.koushikdutta.ion:ion:2.2.1'
    implementation 'org.jsoup:jsoup:1.11.3'
}
